plugins {
  id "org.asciidoctor.jvm.convert"
}

repositories {
  mavenCentral()
}

asciidoctor {
  sourceDir file('build/docs')
}

import groovy.json.JsonSlurper

import java.time.LocalDate

task buildIndex {
  doLast {
    def titles = file("build/staging").listFiles()
        .findAll { it.isDirectory() }
        .collectEntries { [it.name, new JsonSlurper().parse(file("${it}/metadata.json"))] }
        .toSorted { a, b -> b.value.date <=> a.value.date }
        .groupBy { LocalDate.parse(it.value.date).year }

    file('build/docs').mkdirs()
    PrintWriter writer = new PrintWriter(file('build/docs/index.adoc').newWriter())
    writer.append """\
# nflx-slidedecks
:author: Rahul Somasunderam
:experimental:

On HTML Slidedecks, hit kbd:[s] to open speaker notes in a new window.
Hit kbd:[?] to see all shortcuts.

"""
    titles.each { year, map ->
      writer.printf("## %s%n", year)
      writer.println("")
      writer.println('[cols="2a,3a,7a"]')
      writer.println("|===")
      map.each { k, v ->
        writer.printf("| %s | %s | %s - link:%s/html/index.html[HTML] - link:%s/pdf/index.pdf[PDF] - %s[Video]%n",
                v.date, v.venue, v.title, k, k, v.video)
      }
      writer.println("|===")
    }
    writer.flush()
    writer.close()
  }
  outputs.file('build/docs/index.adoc')
}

tasks.asciidoctor.dependsOn('buildIndex')
tasks.build.dependsOn('asciidoctor')
tasks.build.doLast {
  copy {
    from('build/docs/asciidoc')
    into('build/staging')
  }
  println "Open ${buildDir}/staging/index.html"
}
