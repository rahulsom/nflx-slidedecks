apply plugin: "org.asciidoctor.jvm.revealjs"
apply plugin: "org.asciidoctor.decktape"

repositories {
    mavenCentral()
    ruby { gems() }
}

revealjs {
    version = '3.1.0'

    templateGitHub {
        organisation = 'hakimel'
        repository = 'reveal.js'
        tag = '3.9.1'
    }
}

asciidoctorRevealJs {
    revealjsOptions { o ->
        o.controls = false
        overviewMode = true
        progressBar = true
        pushToHistory = true
        customTheme = "build/sass/netflix.css"
        verticalCenter = false
    }
}

asciidoctorj {
    modules {
        diagram.use()
    }
    attributes 'includedir': "${projectDir}/src/docs/asciidoc"
}

task copyStyles(dependsOn: ':stylesheet:sassCompile') {
    doLast {
        copy {
            from "${rootDir}/stylesheet/build/sass"
            into "build/sass"
        }
        copy {
            from "${rootDir}/stylesheet/src/main/sass"
            into "${buildDir}/docs/asciidocRevealJs/style"
        }
        copy {
            from "${rootDir}/stylesheet/build/docs/asciidocRevealJs/package"
            into "${buildDir}/docs/asciidocRevealJs/package"
        }
    }
    inputs.dir("${rootDir}/stylesheet/build/sass")
    inputs.dir("${rootDir}/stylesheet/src/main/sass")
    outputs.dir("build/sass")
    outputs.dir("${buildDir}/docs/asciidocRevealJs/style")
}

tasks.asciidoctorRevealJs.dependsOn('copyStyles')
tasks.asciidoctorRevealJs.doLast {
    println "Open ${buildDir}/docs/asciidocRevealJs/index.html"
}

task copyToPages {
    doLast {
        copy {
            from "build/docs/asciidocRevealJs"
            into "${rootDir}/pages/build/staging/${project.name}/html"
        }
        copy {
            from "build/docs/asciidocRevealJsExport"
            into "${rootDir}/pages/build/staging/${project.name}/pdf"
        }
        file("${rootDir}/pages/build/staging/${project.name}").mkdirs()
        file("${rootDir}/pages/build/staging/${project.name}/title.txt").text = project.description
    }
    inputs.dir "build/docs/asciidocRevealJs"
    outputs.dir "${rootDir}/pages/build/staging/${project.name}/html"
    inputs.dir "build/docs/asciidocRevealJsExport"
    outputs.dir "${rootDir}/pages/build/staging/${project.name}/pdf"
    dependsOn 'asciidoctorRevealJsExport'
}

tasks.build.dependsOn('asciidoctorRevealJsExport', 'copyToPages')
tasks.getByPath(':pages:buildIndex').dependsOn(":${project.name}:copyToPages")
